apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
def Properties props = new Properties()
def apkTime = getTimeSuffix()
props.load(new FileInputStream(file("signing.properties")))
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.allen.supertextview"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
            keyAlias props['KEYSTORE_ALIAS']
            keyPassword props['KEY_PASSWORD']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //debuggable false时,release包不能连接电脑进行调试,BuildConfig.DEBUG的值就是false
            //debuggable true时,release包能连接电脑进行调试,BuildConfig.DEBUG的值就是true
            debuggable false
            //shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            debuggable true
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "STV_${defaultConfig.versionName}_${apkTime}_${defaultConfig.versionCode}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.zhy:base-rvadapter:3.0.3'
    implementation 'com.github.lygttpod:SuperTextView:2.2.1'
}
//apk包的后缀。年月日，例如20181203174200。
def getTimeSuffix() {
    def date = new Date()
    def time = date.format('yyyyMMddHHmmss')
    return time
}
